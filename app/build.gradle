apply plugin: 'com.android.application'

/**
 * Get Google Map API Key which should be used by google map services.
 *
 * Note that this key should not be used when an key is available from Jenkins as most probably debug.keystore is
 * different than the one on your machine.
 *
 * See http://stackoverflow.com/questions/37463523/android-build-gradle-replace-string-resource-value
 *
 * @return the value of your local Google Map API Key
 */
def getLocalGoogleMapApiKey() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def localMalKey = properties.getProperty('google.map.apiKey')
    if (localMalKey == null) {
        // Throw an exception if the property wasn't setup for this
        throw new GradleException(
                "Google Map API Key property not found. Define your debug Google Map API Key [google.map.apiKey] in " +
                        "your local.properties file! E.g.: google.map.apiKey=YOUR_GOOGLE_MAP_API_KEY")
    }
    return localMalKey
}

// Google Map API Key - from Jenkins or from local
String googleMapApiKey = System.env.MAP_API_KEY ?: getLocalGoogleMapApiKey();

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"
    defaultConfig {
        applicationId "com.pepperpop.skansen"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            // Create a string resource which will be used in AndroidManifest.xml file
            resValue "string", "google_maps_key", googleMapApiKey
            println(">>>>>>>>> Google Map API Key: " + googleMapApiKey)
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.google.firebase:firebase-core:9.6.1'
    compile 'com.google.firebase:firebase-auth:9.6.1'
    compile 'com.google.android.gms:play-services-auth:9.6.1'
    compile 'com.google.firebase:firebase-database:9.6.1'
    compile 'com.google.android.gms:play-services:9.6.1'
    testCompile 'junit:junit:4.12'
}

// Add to the bottom of the file
apply plugin: 'com.google.gms.google-services'
